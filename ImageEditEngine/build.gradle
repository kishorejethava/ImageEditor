apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven' // ADD THIS
apply plugin: "com.jfrog.bintray"
group='com.github.kishorejethava'

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    vectorDrawables.useSupportLibrary = true
    ndk {
      moduleName "photoprocessing"
    }
  }
  sourceSets.main {
    jni.srcDirs = []
    jniLibs.srcDir 'src/main/libs'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'com.android.support.constraint:constraint-layout:1.1.0'
  implementation ('com.android.support:support-annotations:27.1.1')
  implementation 'com.android.support:support-v4:27.1.1'
  testImplementation 'junit:junit:4.12'
  testImplementation "org.robolectric:robolectric:3.3.2"
  implementation "com.android.support:design:27.1.1"
  implementation 'com.android.support:appcompat-v7:27.1.1'
  api 'com.github.bumptech.glide:glide:4.6.1'
  implementation 'com.android.support:recyclerview-v7:27.1.1'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext {
  bintrayRepo = 'maven'
  bintrayName = 'PhotoEdiitor'

  publishedGroupId = 'com.android.photoeditor'
  libraryName = 'PhotoEdiitor'
  artifact = 'photoediitor'

  libraryDescription = 'Image editor library with features like filter effect, cropping, Emojicon, Text overlay and Free writting on Image.'

  siteUrl = 'https://github.com/kishorejethava/PhotoEditor'      // Homepage URL of the library
  gitUrl = 'https://github.com/kishorejethava/PhotoEditor.git'

  libraryVersion = '1.0.1'

  developerId = 'kishorejethava'
  developerName = 'Kishore Jethava'
  developerEmail = 'kishorejethava91@gmail.com'

  licenseName = 'The Apache Software License, Version 2.0'
  licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
  allLicenses = ["Apache-2.0"]
}

group = publishedGroupId
version = libraryVersion

install {
  repositories.mavenInstaller {
    pom.project {
      packaging 'aar'
      groupId publishedGroupId
      artifactId artifact

      name libraryName
      description libraryDescription
      url siteUrl

      licenses {
        license {
          name licenseName
          url licenseUrl
        }
      }
      developers {
        developer {
          id developerId
          name developerName
          email developerEmail
        }
      }
      scm {
        connection gitUrl
        developerConnection gitUrl
        url siteUrl
      }
    }
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ['archives']
  pkg {
    repo = bintrayRepo
    name = bintrayName
    desc = libraryDescription
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = allLicenses
    dryRun = false
    publish = true
    override = false
    publicDownloadNumbers = true
    version {
      desc = libraryDescription
    }
  }
}

